Program ::= Declaration* Function* Class* 'main:' Block
SimpleType ::= ('int' | 'float' | 'string' | 'char' | 'bool')
ComposedType ::= SimpleType '[' IntLiteral ']' | 'id'
Class ::= 'class' 'id' ('(' 'id' ')')? ':' InitBlock Function*
InitBlock ::= 'init' ':' ('let' 'id' ':' ComposedType)* Constructor
Constructor ::= 'id' '(' ('id' ':' Composedtype (',' 'id' ':' ComposedType)*)? ')' ':' ConstructorBlock
ConstructorBlock ::= 'super' '(' ('id' (',' 'id')*)? ')' ConstructorStatement* | ConstructorStatement+
Function ::= 'def' 'id' '(' ('id' ':' SimpleType ( ',' 'id' ':' SimpleType)*)? ')' '->' (SimpleType | 'void') ':' Block
ConstructorStatement ::= Pass | Assignment
CreateObject ::= 'create' 'id' ':' 'id' '(' ('id' (',' 'id')*)? ')'
Statement ::= FunctionCall | MethodCall | Declaration | CreateObject | Assignment | If | While  | For | Print | Read | 'return' Expr? | 'continue' | 'pass' | 'break'
Assignment ::= ('id' | 'self') ('.' 'id')? ('[' Expr ']')? ':=' Expr
Declaration ::= 'let' 'id' (',' 'id')* ':' ComposedType (':=' Expr)?
Print ::= 'print' '(' Expr ')'
Read ::= 'read' '(' ')'
FunctionCall ::= 'id' '(' (Expr (',' Expr)*)? ')'
MethodCall ::= ('id' | 'self') '.' 'id' '(' (Expr (',' Expr)*)? ')'
MemberAccess ::= ('id' | 'self') '.' 'id' ('[' Expr ']')?
Block ::= Statement+
If ::= 'if' Expr ':' Block ('elif' Expr ':' Block)* ('else' Block)?
While ::= 'while' Expr ':' Block
For ::= 'for' 'id' (',' 'id')* ':' SimpleType ':=' Expr ':' Expr ':' Assignment+ ':' Block
Expr ::= AndExpr 'or' Expr | AndExpr
AndExpr ::= NotExpr 'and' AndExpr | NotExpr
NotExpr ::= 'not'? CExpr
CExpr ::= FirstLevel ('==' | '!=' | '>' | '<' | '<=' | '>=') CExpr | FirstLevel
FirstLevel ::= SecondLevel ('+' | '-') FirstLevel | SecondLevel
SecondLevel ::= ThirdLevel ('*' | '/') SecondLevel | ThirdLevel
ThirdLevel ::= FourthLevel '**' ThirdLevel | FourthLevel
FourthLevel ::= ('+' | '-')? Factor
Factor ::= 'id' | FunctionCall | MethodCall | ArrayAccess | MemberAccess | '(' Expr ')' | Integer | Float | Bool | String
Integer ::= IntLiteral
Float ::= FloatLiteral
Bool ::= 'True' | 'False'
ArrayAccess ::= 'id' '[' Expr ']'
