Program ::= Declaration* Function* Class* 'main:' Block
SimpleType ::= ('int' | 'float' | 'string' | 'bool')
ComposedType ::= SimpleType '[' IntLiteral ']' | 'id'
Class ::= 'class' 'id' ('(' 'id' ')')? ':' ('members:' ('id' (',' 'id')* ':' SimpleType ('[' IntLiteral ']')?)+)* Function+
Function ::= 'def' 'id' '(' ('id' ':' SimpleType ( ',' 'id' ':' SimpleType)*)? ')' '->' (SimpleType | 'void') ':' Block
Statement ::= FunctionCall | MethodCall | Declaration | Assignment | If | While  | For | Print | Read | 'return' Expr? | 'continue' | 'pass' | 'break'
Assignment ::= 'id' ('.' 'id')? ('[' Expr ']')? ':=' Expr
Declaration ::= 'let' 'id' (',' 'id')* ':' ComposedType (':=' Expr)?
Print ::= 'print' '(' Expr ')'
Read ::= 'read' '(' ')'
FunctionCall ::= 'id' '(' (Expr (',' Expr)*)? ')'
MethodCall ::= ('id' | 'self') '.' 'id' '(' (Expr (',' Expr)*)? ')'
MemberAccess ::= ('id' | 'self') '.' 'id' ('[' Expr ']')?
Block ::= Statement+
If ::= 'if' Expr ':' Block ('elif' Expr ':' Block)* ('else' Block)?
While ::= 'while' Expr ':' Block
For ::= 'for' Declaration ':' Expr ':' Assignment ':' Block
Expr ::= AndExpr 'or' Expr | AndExpr
AndExpr ::= CExpr 'and' AndExpr | CExpr
CExpr ::= FirstLevel ('==' | '!=' | '>' | '<' | '<=' | '>=') Expr | FirstLevel
FirstLevel ::= SecondLevel ('+' | '-') FirstLevel | SecondLevel
SecondLevel ::= ThirdLevel ('*' | '/') SecondLevel | ThirdLevel
ThirdLevel ::= Factor '**' ThirdLevel | Factor
Factor ::= 'id' | FunctionCall | MethodCall | ArrayAccess | MemberAccess | 'not' Expr | '(' Expr ')' | Integer | Float | Bool | String
Integer ::= '-'? IntLiteral
Float ::= '-'? FloatLiteral
Bool ::= 'True' | 'False'
ArrayAccess ::= 'id' '[' Expr ']'
